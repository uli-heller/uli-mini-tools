defaultTasks 'dist'

apply plugin: 'project-report'

// Configure compiler options and other things - taken from
// https://github.com/spring-projects/spring-framework/blob/master/build.gradle
configure(subprojects) { project ->
        apply plugin: 'eclipse'
        ext.gradleScriptDir = "${rootProject.projectDir}"
        ext.scriptsDir = "${rootProject.projectDir}/scripts"
        ext.distSh = "${scriptsDir}/dist.sh"
        ext.distBat = "${scriptsDir}/dist.bat"
        ext.commonGradle    = "${rootProject.projectDir}/common.gradle"

        def defaultDistTasks = [ 'sh', 'bat' ];
        def specialDistTasks = [
	  util: ['jar', 'test'],
        ];

	apply plugin: 'java'
	
	apply from: "${commonGradle}"
 
	[compileJava, compileTestJava]*.options*.compilerArgs = [
		"-Xlint:serial",
		"-Xlint:varargs",
		"-Xlint:cast",
		"-Xlint:classfile",
		"-Xlint:dep-ann",
		"-Xlint:divzero",
		"-Xlint:empty",
		"-Xlint:finally",
		"-Xlint:overrides",
		"-Xlint:path",
		"-Xlint:processing",
		"-Xlint:static",
		"-Xlint:try",
		"-Xlint:unchecked",
		"-Xlint:-options",     // intentionally disabled
		"-Xlint:-fallthrough", // intentionally disabled
		"-Xlint:-rawtypes",    // TODO enable and fix warnings
		"-Xlint:-deprecation"  // TODO enable and fix warnings
	]
	task dist(dependsOn: specialDistTasks[name] ?: defaultDistTasks);
        clean.dependsOn cleanEclipse;
        clean {
          delete "${projectDir}/${project.name}-${version}.sh"
          delete "${projectDir}/${project.name}-${version}.bat"
          delete "${projectDir}/${project.name}*log"
          delete "${buildDir}"
        }
        test {
          reports.html.enabled = false
        }
}

configure(allprojects) { project ->
  ext.groovyVersion = '2.3.7'
}

htmlDependencyReport {
  projects = project.allprojects
}

task testReport(type: TestReport) {
    destinationDir = file("$buildDir/reports/allTests")
    // Include the results from the `test` task in all subprojects
    reportOn subprojects*.test
}

task wrapper(type: Wrapper) {
  gradleVersion = "2.2-rc-1" //"2.0"
}
